/* 
	void mmxthreshold( unsigned char* in, unsigned char* out, ASMINT count, unsigned char lower, unsigned char upper)

	register usage:
		esi: data   (in, uint8_t*)
		edi: result (in, uint8_t*)
		eax: upper  (in, uint8_t), later used as index (0)
		edx: lower  (in, uint8_t)
		ecx: count  (in, uint32_t)

*/

; .intel_syntax noprefix
; .globl mmx_threshold
; .hidden mmx_threshold
; mmx_threshold:

movd mm3, eax  /* 8 bytes with upper threshold in mm3 */
punpcklbw mm3, mm3
punpcklbw mm3, mm3
punpcklbw mm3, mm3

xor pax, pax   /* eax now used as index register, starts at 0 */

movd mm1, edx  /* 8 bytes with lower threshold in mm1 */
punpcklbw mm1, mm1
punpcklbw mm1, mm1
punpcklbw mm1, mm1

mov  edx, 128
movd mm2, edx  /* 8 bytes with MSB set in mm2 */
punpcklbw mm2, mm2
punpcklbw mm2, mm2
punpcklbw mm2, mm2

pxor mm1, mm2 /* convert unsigned->signed by flipping each MSB */
pxor mm3, mm2

mmx_threshold_loop:
	movq mm0, [psi+pax]  /* load 8 bytes to mm0 */
	pxor mm0, mm2        /* convert unsigned->signed by flipping each MSB */
	movq mm4, mm3        /* store copy of signed value for upper threshold comp. */
	pcmpgtb mm4, mm0     /* signed compare of mm0 and mm4 */
	pcmpgtb mm0, mm1     /* signed compare of mm1 and mm0 */
	pand mm0, mm4        /* AND of result bytes in mm0 */
	movq [pdi+pax], mm0  /* store 8 bytes from mm0  */
	add pax,   8         /* FIXME: fixed step width */
	cmp pax, pcx
	jb mmx_threshold_loop

emms
; ret

